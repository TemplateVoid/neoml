/* Copyright Â© 2017-2020 ABBYY Production LLC

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

	http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
--------------------------------------------------------------------------------------------------------------*/

layout(std430, binding=1) readonly buffer Source { float source[]; };
layout(std430, binding=2) writeonly buffer Result { float result[]; };

void main()
{
	int xy = ID_X;
	int c = ID_Y;
	
	if( xy >= P.size || c >= P.sourceChannels ) {
		return;
	}

	int resultPos = xy * P.filterObjectSize + c;

	xy += P.offset;
	int x = xy % P.resultWidth;
	int y = xy / P.resultWidth;
	
	int startX = P.stride.x * x - P.padding.x;
	int startY = P.stride.y * y - P.padding.y;
	int inputY = startY;
	for( int fy = 0; fy < P.filterHeight; fy++ ) {
		if( 0 <= inputY && inputY < P.sourceHeight ) {
			int inputX = startX;
			int sourcePos = c + inputY * P.sourceWidth * P.sourceChannels;
			for( int fx = 0; fx < P.filterWidth; fx++ ) {
				if( 0 <= inputX && inputX < P.sourceWidth ) {
					result[resultPos] = source[sourcePos + inputX * P.sourceChannels];
				} else {
					result[resultPos] = 0;
				}
				resultPos += P.sourceChannels;
				inputX += P.dilation.x;
			}
		} else {
			for( int fx = 0; fx < P.filterWidth; fx++ ) {
				result[resultPos] = 0;
				resultPos += P.sourceChannels;
			}
		}
		inputY += P.dilation.y;
	}
}
